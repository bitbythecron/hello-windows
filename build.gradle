plugins {
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id 'java'
  id 'edu.sc.seis.launch4j' version '2.4.6'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile(
        'ch.qos.logback:logback-classic:1.2.3'
        ,'org.slf4j:jul-to-slf4j:1.7.25'
        ,'org.apache.commons:commons-lang3:3.7'
        ,'commons-io:commons-io:2.6'
        ,'org.apache.commons:commons-text:1.2'
        ,'com.google.guava:guava:23.0'
        ,'commons-beanutils:commons-beanutils-core:1.8.3'
        ,'org.codehaus.groovy:groovy:2.4.12'

        ,'org.projectlombok:lombok:1.18.8'

        ,'com.jgoodies:jgoodies-forms:1.9.0'

        ,'com.google.inject:guice:4.2.2'

    )

    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile(
        'junit:junit:4.12'
        ,'org.mockito:mockito-core:2.23.0'
    )

}

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

shadowJar {
   baseName = 'hello-windows'
   classifier = null
   version = null
   manifest {
    attributes(
      'Main-Class': 'com.example.windows.hello.HelloWindowsApp'
    )
  }
}

launch4j {
    mainClassName = 'com.example.windows.hello.HelloWindowsApp'
    icon = "${projectDir}/icon.ico"
    jdkPreference = 'jdkOnly'
    initialHeapSize = 128
    jreMinVersion = '1.8.0'
    jreMaxVersion = '1.8.9'
    maxHeapSize = 512
    stayAlive = false
    bundledJre64Bit = true
    bundledJrePath = "${projectDir}/libs/jre8/bin/javaw.exe"
}

task createDistro(type: Zip) {
    from("$buildDir/launch4j") {
        include "*.exe"
    }

    from("$buildDir/launch4j") {
        include "lib/*" into "lib"
    }

    from("libs/jre8") {
        include "*/*" into "jre8"
    }

    archiveName = "hello-windows.zip"
    destinationDir = file("$buildDir/dist")

}
